// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "../OwnableExt.sol";
import "@openzeppelin/contracts/utils/Counters.sol";

contract OZToken is ERC721, ERC721Enumerable, ERC721URIStorage, OwnableExt {
    using Counters for Counters.Counter;

    Counters.Counter private _tokenIdCounter;

    uint256 private mintPrice;

    constructor(uint256 _mintPrice) ERC721("OZ Token", "OZT") {
        mintPrice = _mintPrice;
    }

    function mint(uint256 _amount) external payable {
        require(msg.value == _amount * mintPrice, "INCORRECT_FUND_AMOUNT");
        for (uint256 i = 0; i < _amount; i++) {
            uint256 tokenId = _tokenIdCounter.current();
            _tokenIdCounter.increment();
            _mint(msg.sender, tokenId);
        }
    }

    function safeMint(uint256 _amount) external payable {
        require(msg.value == _amount * mintPrice, "INCORRECT_FUND_AMOUNT");
        for (uint256 i = 0; i < _amount; i++) {
            uint256 tokenId = _tokenIdCounter.current();
            _tokenIdCounter.increment();
            _safeMint(msg.sender, tokenId);
        }
    }

    function burn(uint256 _tokenId) external {
        require(msg.sender == ownerOf(_tokenId));
        _burn(_tokenId);
    }

    function withdrawFunds() external payable onlyOwner {
        (bool sent, ) = msg.sender.call{value: address(this).balance}("");
        require(sent, "Failed to send Ether");
    }

    // Function to receive Ether. msg.data must be empty
    receive() external payable {}

    // Fallback function is called when msg.data is not empty
    fallback() external payable {}

    // The following functions are overrides required by Solidity.

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal override(ERC721, ERC721Enumerable) {
        super._beforeTokenTransfer(from, to, tokenId);
    }

    function _burn(uint256 tokenId) internal override(ERC721, ERC721URIStorage) {
        super._burn(tokenId);
    }

    function tokenURI(uint256 tokenId)
        public
        view
        override(ERC721, ERC721URIStorage)
        returns (string memory)
    {
        return super.tokenURI(tokenId);
    }

    function supportsInterface(bytes4 interfaceId)
        public
        view
        override(ERC721, ERC721Enumerable)
        returns (bool)
    {
        return super.supportsInterface(interfaceId);
    }

    function getMintPrice() external view returns (uint256) {
        return mintPrice;
    }
}
